
#include <SD.h>

// This is calibration data for the raw touch data to the screen coordinates
#define TS_MINX 3800
#define TS_MAXX 100
#define TS_MINY 100
#define TS_MAXY 3750

// Size of the color selection boxes and the paintbrush size
#define BOXSIZE 40
#define PENRADIUS 3

enum SwipeDirection
{
    UP,
    DOWN,
    LEFT,
    RIGHT,
    NO_SWIPE
};

class TftFeatherWing
{
    public:
    
    TftFeatherWing()
    {
        
    }
 
    SwipeDirection xSwipeCheck(int currentX, int currentY)
    {
        SwipeDirection swiped = NO_SWIPE;

        if(oldX != currentX)
        {
            // the position has changed

            if(currentX > oldX)
            {
                // the X point moved in the down direction (from the SD card slot toward the reset button)
                xDownCount++;

                if(xDownCount >= SWIPE_DELTA_THRESHOLD)
                {
                    swiped = SwipeDirection::DOWN;

                    Serial.print("swipe down detected; x=");
                    Serial.println(currentX);

                    // reset the down count, now that swipe was detected
                    resetSwipeIncrementCounts();
                }
            }
            else
            {
                // the X point moved in the up direction (from the reset button toward the SD card slot)
                xUpCount++;

                if(xUpCount >= SWIPE_DELTA_THRESHOLD)
                {
                    swiped = SwipeDirection::UP;

                    Serial.print("swipe UP detected; x = ");
                    Serial.println(currentX);

                    // reset the up count, now that the swipe was detected
                    resetSwipeIncrementCounts();
                }
            }
        }

        // check for Y swipe    
        if(oldY != currentY)
        {
            // the position has changed

            if(currentY > oldY)
            {
                // the Y point moved in the right? direction (from the SD card slot toward the reset button)
                yDownCount++;

                if(yDownCount >= SWIPE_DELTA_THRESHOLD)
                {
                    swiped = SwipeDirection::RIGHT;

                    Serial.print("swipe right? detected; y=");
                    Serial.println(currentY);

                    // reset the down count, now that swipe was detected
                    resetSwipeIncrementCounts();
                }
            }
            else
            {            
                // the Y point moved in the left? direction (from the reset button toward the SD card slot)
                yUpCount++;

                if(yUpCount >= SWIPE_DELTA_THRESHOLD)
                {
                    swiped = SwipeDirection::LEFT;

                    Serial.print("swipe left? detected; y = ");
                    Serial.println(currentY);

                    // reset the up count, now that the swipe was detected
                    resetSwipeIncrementCounts();
                }
            }
        }

        // adjust the old X position for the next loop iteration
        oldX = currentX;
        oldY = currentY;

        return swiped;
    }    
    
    private:

    int SWIPE_DELTA_THRESHOLD = 30;
        
    int oldX = 0;
    int oldY = 0;

    int xDownCount = 0;
    int yDownCount = 0;

    int xUpCount = 0;
    int yUpCount = 0;
    
    void resetSwipeIncrementCounts()
    {
        xDownCount = 0;
        yDownCount = 0;

        xUpCount = 0;
        yUpCount = 0;    
    }
};

//int const SWIPE_LENGTH = 20;

Adafruit_ILI9341 tft = Adafruit_ILI9341(TFT_CS, TFT_DC);
/**
 * Setup the touch screen.
 */
Adafruit_STMPE610 ts = Adafruit_STMPE610(STMPE_CS);


int oldcolor;
int currentcolor;

void setupTft()
{
    if (!ts.begin()) 
    {
      Serial.println("Couldn't start touchscreen controller");
      while (1);
    }
    Serial.println("Touchscreen started");
    
    tft.begin();
    tft.fillScreen(ILI9341_BLACK);

    // make the color selection boxes
    tft.fillRect(0, 0, BOXSIZE, BOXSIZE, ILI9341_RED);
    tft.fillRect(BOXSIZE, 0, BOXSIZE, BOXSIZE, ILI9341_YELLOW);
    tft.fillRect(BOXSIZE*2, 0, BOXSIZE, BOXSIZE, ILI9341_GREEN);
    tft.fillRect(BOXSIZE*3, 0, BOXSIZE, BOXSIZE, ILI9341_CYAN);
    tft.fillRect(BOXSIZE*4, 0, BOXSIZE, BOXSIZE, ILI9341_BLUE);
    tft.fillRect(BOXSIZE*5, 0, BOXSIZE, BOXSIZE, ILI9341_MAGENTA);

    // select the current color 'red'
    tft.drawRect(0, 0, BOXSIZE, BOXSIZE, ILI9341_WHITE);
    currentcolor = ILI9341_RED;
}

// These read 16- and 32-bit types from the SD card file.
// BMP data is stored little-endian, Arduino is little-endian too.
// May need to reverse subscript order if porting elsewhere.

uint16_t read16(File &f) {
  uint16_t result;
  ((uint8_t *)&result)[0] = f.read(); // LSB
  ((uint8_t *)&result)[1] = f.read(); // MSB
  return result;
}

uint32_t read32(File &f) {
  uint32_t result;
  ((uint8_t *)&result)[0] = f.read(); // LSB
  ((uint8_t *)&result)[1] = f.read();
  ((uint8_t *)&result)[2] = f.read();
  ((uint8_t *)&result)[3] = f.read(); // MSB
  return result;
}

/**
// This function opens a Windows Bitmap (BMP) file and
// displays it at the given coordinates.  It's sped up
// by reading many pixels worth of data at a time
// (rather than pixel by pixel).  Increasing the buffer
// size takes more of the Arduino's precious RAM but
// makes loading a little faster.  20 pixels seems a
// good balance.
*/
#define BUFFPIXEL 20
void bmpDraw(char *filename, uint8_t x, uint16_t y) 
{

  File     bmpFile;
  int      bmpWidth, bmpHeight;   // W+H in pixels
  uint8_t  bmpDepth;              // Bit depth (currently must be 24)
  uint32_t bmpImageoffset;        // Start of image data in file
  uint32_t rowSize;               // Not always = bmpWidth; may have padding
  uint8_t  sdbuffer[3*BUFFPIXEL]; // pixel buffer (R+G+B per pixel)
  uint8_t  buffidx = sizeof(sdbuffer); // Current position in sdbuffer
  boolean  goodBmp = false;       // Set to true on valid header parse
  boolean  flip    = true;        // BMP is stored bottom-to-top
  int      w, h, row, col;
  uint8_t  r, g, b;
  uint32_t pos = 0, startTime = millis();

  if((x >= tft.width()) || (y >= tft.height())) return;

  Serial.println();
  Serial.print(F("Loading image '"));
  Serial.print(filename);
  Serial.println('\'');
  Serial.println();

  // Open requested file on SD card
  if ((bmpFile = SD.open(filename)) == NULL) {
    Serial.print(F("File not found"));
    return;
  }

  // Parse BMP header
  if(read16(bmpFile) == 0x4D42) { // BMP signature
    Serial.print(F("File size: ")); Serial.println(read32(bmpFile));
    (void)read32(bmpFile); // Read & ignore creator bytes
    bmpImageoffset = read32(bmpFile); // Start of image data
    Serial.print(F("Image Offset: ")); Serial.println(bmpImageoffset, DEC);
    // Read DIB header
    Serial.print(F("Header size: ")); Serial.println(read32(bmpFile));
    bmpWidth  = read32(bmpFile);
    bmpHeight = read32(bmpFile);
    if(read16(bmpFile) == 1) { // # planes -- must be '1'
      bmpDepth = read16(bmpFile); // bits per pixel
      Serial.print(F("Bit Depth: ")); Serial.println(bmpDepth);
      if((bmpDepth == 24) && (read32(bmpFile) == 0)) { // 0 = uncompressed

        goodBmp = true; // Supported BMP format -- proceed!
        Serial.print(F("Image size: "));
        Serial.print(bmpWidth);
        Serial.print('x');
        Serial.println(bmpHeight);

        // BMP rows are padded (if needed) to 4-byte boundary
        rowSize = (bmpWidth * 3 + 3) & ~3;

        // If bmpHeight is negative, image is in top-down order.
        // This is not canon but has been observed in the wild.
        if(bmpHeight < 0) {
          bmpHeight = -bmpHeight;
          flip      = false;
        }

        // Crop area to be loaded
        w = bmpWidth;
        h = bmpHeight;
        if((x+w-1) >= tft.width())  w = tft.width()  - x;
        if((y+h-1) >= tft.height()) h = tft.height() - y;

        // Set TFT address window to clipped image bounds
        tft.setAddrWindow(x, y, x+w-1, y+h-1);

        for (row=0; row<h; row++) { // For each scanline...

          // Seek to start of scan line.  It might seem labor-
          // intensive to be doing this on every line, but this
          // method covers a lot of gritty details like cropping
          // and scanline padding.  Also, the seek only takes
          // place if the file position actually needs to change
          // (avoids a lot of cluster math in SD library).
          if(flip) // Bitmap is stored bottom-to-top order (normal BMP)
            pos = bmpImageoffset + (bmpHeight - 1 - row) * rowSize;
          else     // Bitmap is stored top-to-bottom
            pos = bmpImageoffset + row * rowSize;
          if(bmpFile.position() != pos) { // Need seek?
            bmpFile.seek(pos);
            buffidx = sizeof(sdbuffer); // Force buffer reload
          }

          for (col=0; col<w; col++) { // For each pixel...
            // Time to read more pixel data?
            if (buffidx >= sizeof(sdbuffer)) { // Indeed
              bmpFile.read(sdbuffer, sizeof(sdbuffer));
              buffidx = 0; // Set index to beginning
            }

            // Convert pixel from BMP to TFT format, push to display
            b = sdbuffer[buffidx++];
            g = sdbuffer[buffidx++];
            r = sdbuffer[buffidx++];
            tft.pushColor(tft.color565(r,g,b));
          } // end pixel
        } // end scanline
        Serial.print(F("Loaded in "));
        Serial.print(millis() - startTime);
        Serial.println(" ms");
      } // end goodBmp
    }
  }

  Serial.println("TFW - closing bmpFile");
  bmpFile.close();
  if(!goodBmp) Serial.println(F("BMP format not recognized."));
}